/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.0/userguide/tutorial_java_projects.html
 */

/*
// Apply the java plugin to add support for Java
apply plugin: 'java'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.24'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}
*/
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

group 'com.liuwill.data-structure'
version '1.0-SNAPSHOT'

ext.junitPlatformVersion = '1.0.1'
ext.junitJupiterVersion  = '5.0.1'
ext.log4jVersion         = '2.6.2'
ext.logbackVersion       = '1.2.3'
ext.junit4Version        = '4.12'
ext.junitVintageVersion  = '4.12.1'
ext {
    p3c = "1.3.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
// apply plugin: 'com.github.ksoichiro.console.reporter'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

mainClassName = 'com.liuwill.kata.DataStructure'

sourceSets{
    main{
        java {srcDir "src/main/java"}
        resources {srcDir "src/main/resources"}
    }
    test{
        java {srcDir "src/test/java"}
        resources {srcDir "src/test/resources"}
    }
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.1")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.1")

    // JUnit Jupiter API and TestEngine implementation
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

    testCompile("org.junit.platform:junit-platform-runner:${junitPlatformVersion}")

    // If you also want to support JUnit 3 and JUnit 4 tests
    testCompile("junit:junit:${junit4Version}")
    testRuntime("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}")

    testRuntime("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    testRuntime("org.apache.logging.log4j:log4j-jul:${log4jVersion}")
    testCompile("ch.qos.logback:logback-classic:${logbackVersion}")

    pmd "com.alibaba.p3c:p3c-pmd:${p3c}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task createJavaProject {
    doLast {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs()}
    }
}

junitPlatform {
    platformVersion '1.0.1'
    filters {
        engines {
            // include 'junit-jupiter', 'junit-vintage'
            // exclude 'custom-engine'
        }
        tags {
            // include 'fast'
            exclude 'slow'
        }
        // includeClassNamePattern '.*Test'
    }
    enableStandardTestTask true
    // reportsDir file('build/test-results/junit-platform') // this is the default
    logManager 'org.apache.logging.log4j.jul.LogManager'
    // reportsDir file('build/test-results/junit-platform') // this is the default
    // enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}

// [ FindBugs Start**/
findbugs{
    ignoreFailures=true
    findbugsTest.enabled=false
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
// ] FindBugs End**/
checkstyle{
    toolVersion = '7.8.2'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        // html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
    configFile = file('config/checkstyle/checkstyle.xml')
}

junitPlatform.enableStandardTestTask true

jacoco{
    toolVersion = "0.7.9"
    applyTo junitPlatformTest
    // reportsDir = file("$buildDir/customJacocoReportDir")
}

junitPlatformTest {
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}

jacocoTestReport {
    executionData junitPlatformTest
    sourceSets sourceSets.main
    sourceDirectories = files(project.sourceSets.main.allSource.srcDirs)
    classDirectories = files(project.sourceSets.main.output)

    reports{
        xml.enabled = true
        html.enabled = true

        // html.destination = "${buildDir}/jacocoHtml"
    }
}

task parseCoverageSummary(dependsOn: jacocoTestReport) {
  group = 'ci'
  doLast {
    def testReportFile = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")

    def xmlParser = new XmlParser()
    xmlParser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
    xmlParser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
    def reportDom = xmlParser.parse(testReportFile)
    def coverdInstruct = Integer.parseInt(reportDom.counter[0].@covered)
    def missedInstruct = Integer.parseInt(reportDom.counter[0].@missed)

    def coverageRate = Math.round(coverdInstruct * 10000 / (coverdInstruct + missedInstruct)) / 100
    println ''
    println '=========== Coverage summary ==========='
    println "${project.name} :  ${coverageRate}%"
    println ''

    reportDom.counter.each { counterData ->
      def coverdInstructLine = Integer.parseInt(counterData.@covered)
      def missedInstructLine = Integer.parseInt(counterData.@missed)
      def totalLine = coverdInstructLine + missedInstructLine

      def coverageRateLine = Math.round(coverdInstructLine * 10000 / (coverdInstructLine + missedInstructLine)) / 100
      println "${counterData.@type} :  ${coverageRateLine}%  (${coverdInstructLine}/${totalLine})"
    }
    println '========================================'
  }
}

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  sourceSets sourceSets.main
  sourceDirectories = files(project.sourceSets.main.allSource.srcDirs)
  classDirectories = files(project.sourceSets.main.output)

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    xml.enabled true
    xml.setDestination(file("${buildDir}/reports/jacoco/report.xml"))
    // xml.destination "${buildDir}/reports/jacoco/report.xml"
    html.enabled false
    csv.enabled false
  }
}

codeCoverageReport.dependsOn {
  subprojects*.test
}

pmd {
    consoleOutput = true
    reportsDir = file("build/reports/pmd")

    ruleSets = [
            "java-ali-naming",
            "java-ali-exception",
            "java-ali-constant",
            "java-ali-flowcontrol",
            "java-ali-oop",
            // "java-ali-orm"
    ]
}
